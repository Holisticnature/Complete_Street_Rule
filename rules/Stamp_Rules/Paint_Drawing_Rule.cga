/*
 * File:    Paint_Drawing_Rule.cga
 * Created: 10/25/2018 Oct
 * Last Updated:5/14/2019
 * Author:  David J. Wasserman
 * License: Apache License.
 * Source: https://github.com/d-wasserman/Complete_Street_Rule
# Copyright 2018 David J. Wasserman
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# --------------------------------
 */
 @Hidden
 import cs: "rules/Complete_Street.cga"
 #Attributes
 @Group("Control Setting",1)
 @Order(1)@Range("Asphalt","Green Paint","White Paint","Yellow Paint", "Red Paint")@Description("Describes what is allocated to this one shape.")
 @Enum("Asphalt","Green Paint","White Paint","Yellow Paint", "Red Paint")
 @Handle(shape=Main_Allocation_Start,type=selector,reference=center,slip=inside,occlusion=false,color="#FF6644")
 attr Main_Allocation = "White Paint"
 @Order(2)@Range(0,10) @Description("Height of stamp element.")
 @Handle(shape=Main_Allocation_Start, axis=y,reference=center,align=right,translate={.1,0,0}, slip=screen,color="#6666FF")
 attr Element_Height = cs.sharrowHeight
 @Order(3)@Range("true","false") @Description("If true, this setting will make the component dashed with asphalt every half-meter.")
 @Handle(shape=Main_Allocation_Start,axis=y,reference=center,align=center,translate={-.1,0,0},slip=inside,color ="#2244FF",type=selector)@Enum("true","false")
 attr Dashed_Element = "false"
 @Order(4) @Range(0,2) @Description("If there are dashed elements, this describes the width between them.")
 @Handle(shape=Main_Allocation_Rule,axis=z,reference=center,align=center,translate={-.5,-.2,0},slip=inside,color ="#2244FF",type=linear)
 attr Dashed_Width = .5
 @Order(5) @Range("true","false") @Description("Adds a white paint edge to dashed elements.")
 @Handle(shape=Main_Allocation_Rule,axis=y,reference=right,align=right,translate={-.1,0,0},slip=inside,color ="#22DDFF",type=selector)@Enum("true","false")
 attr Paint_Edge_Element = "true"
 @Order(6) @Range(0,360) @Description("Rotation along Z axis for scope of painted area.")
 attr Z_Rotation = 0
 @Order(7) @Range(0,10) @Description("Setback amount from outside of the shape. Gives a painted shape a white outline.")
 attr Outline_Thickness = 0
 
 
 @Group("DISPLAY OPTIONS",2) @Range("true","false")@Order(1)@Description("When true, textures are on display, when false textures are removed. If true it deletes an unused UVset (see comments for details).")
attr Display_Textures = "true"
@Order(2) @Description("Visually colors the entire street model based on the attributes of a street. Usage thematic looks best when textures are turned off.")
@Range("Thematics Off", "Solid Color", "Peak Runoff/Permeability","Bike Preference","Pedestrian Preference","Auto Preference","Transit Preference","All Mode Preference","Usage","Transit Highlight","Bicycle Highlight","Auto Highlight","Pedestrian Highlight","Planting Highlight","Shared Mobility Highlight","Flood Scenario")
attr Display_Thematics = "Thematics Off" 
@Order(3) @Color @Description("When the Solid Color thematic is used for highlighting certain streets or mode classes, this chooses the color that is utilized by the thematic.") #@Hidden
attr Solid_Color = "#FFFFFF"
@Order(4) @Description("This is the choice of Mode being represented by the chosen surface. Used in reporting and thematics.")
@Range("Bikeways","Pedestrian","Auto","Transit","Shared Mobility","Conflict Zones")
attr Mode_Choice = _default_mode(Main_Allocation)
@Order(5) @Description("Texture used when texturing curbs.") @File ("tif","jpg","png","tiff","gif","jpeg","psp","jsl","sgi","tga","bmp","dds")
attr Custom_Paint_Texture = _paint_path(_find_color(Main_Allocation))
#Functions
_find_color(alloc)=
	case find(alloc,"White",0)!=-1:
		"white"
	case find(alloc,"Red",0)!=-1:
		"red"
	case find(alloc,"Green",0)!=-1:
		"green"
	case find(alloc,"Yellow",0)!=-1:
		"yellow"
	else:
		"white"
		
_default_mode(alloc) =
	case find(alloc,"Red",0)!=-1:
		"Transit"
	case find(alloc,"Green",0)!=-1:
		"Bikeways"
	else:
		"Conflict Zones"

_paint_path(paint_color) = cs.LanesFolder+"/asphalt_painted_" + paint_color + "_7x7m.jpg"
#Constants

const Default_Curb_Edge = cs.SidewalkFolder+"/Concrete Clean Dark.jpg"
#Rules

@StartRule 
Main_Allocation_Start-->
 	set(cs.Display_Textures, Display_Textures)
 	set(cs.Display_Thematics,Display_Thematics)
 	set(cs.Solid_Color,Solid_Color)
 	color(cs._ThematicRouter (Mode_Choice))
 	Setback_Element
 	
Setback_Element--> 
	setback (Outline_Thickness) {all:Main_Allocation_Rule|
			remainder: Dashed_Split_Decision}
 
Dashed_Split_Decision-->
	case bool(Dashed_Element):
		Conflict_Splitter
	else:
 		Main_Allocation_Rule
 		
Conflict_Splitter-->
	alignScopeToGeometry(yUp,largest,0)
	rotateScope(0,Z_Rotation,0)
	split(z) {{~max(Dashed_Width,.1):Dash_Allocation|~max(Dashed_Width,.1):Asphalt("Conflict Zones",cs.sharrowHeight)}*|~max(Dashed_Width,.1):Dash_Allocation}

Dash_Allocation-->
	case bool(Paint_Edge_Element):
		Dash_Edge_Paint
	else:
		Main_Allocation_Rule

Dash_Edge_Paint --> 
	split(x) {cs.PaintLineWidth	:Painted_Asphalt("white")|~1:Main_Allocation_Rule|cs.PaintLineWidth	:Painted_Asphalt("white")}

Main_Allocation_Rule --> 
	case _paint_path(_find_color(Main_Allocation))!= Custom_Paint_Texture:
		Custom_Texture
 	case find(Main_Allocation,"Paint",0)!=-1:
 		Painted_Asphalt(_find_color(Main_Allocation))
 	else:
 		Asphalt("Auto")
 		
Custom_Texture-->
	extrude(world.up,Element_Height)
	Custom_Texture_Stamp

Custom_Texture_Stamp --> 
	setupProjection(0,scope.zx,6,6,0,0,1)
	texture(Custom_Paint_Texture)
	projectUV(0)
	deleteUV(cs._Texture_Switch)
	color(cs._ThematicRouter (Mode_Choice))#If Display Thematics==Usage, goes to usage, if not, Thematic. 
	cs.ModeReporter(Mode_Choice)
 	

Painted_Asphalt(paint_color)-->
	extrude(world.up,Element_Height)
	comp(f) {all:Painted_Asphalt_Stamp(paint_color)}
	
Painted_Asphalt_Stamp(paint_color)-->
	setupProjection(0,scope.zx,6,6,0,0,1)
	texture(_paint_path(paint_color))
	projectUV(0)
	deleteUV(cs._Texture_Switch)
	color(cs._ThematicRouter (Mode_Choice))#If Display Thematics==Usage, goes to usage, if not, Thematic. 
	cs.ModeReporter(Mode_Choice)
 	
Asphalt(Mode)-->
	extrude(world.up,Element_Height)
	comp(f){all:Asphalt_Stamp("Auto")}
Asphalt(Mode,Height)-->
	extrude(world.up,Height)
	comp(f){all:Asphalt_Stamp("Auto")}	
	
Asphalt_Stamp(Mode)-->
	setupProjection(0,scope.zx,14,14,0,0,1)
	texture( cs.LanesFolder+"/asphalt_14x14m.jpg")
	projectUV(0)
	deleteUV(cs._Texture_Switch)
	color(cs._ThematicRouter (Mode))#If Display Thematics==Usage, goes to usage, if not, Thematic-strings might have other purpose later. 
	cs.ModeReporter(Mode)
 